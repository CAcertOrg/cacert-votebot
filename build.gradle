import org.springframework.boot.gradle.plugin.SpringBootPlugin

/*
 * Copyright (c) 2016-2022  Jan Dittberner
 *
 * This file is part of CAcert votebot.
 *
 * CAcert votebot is free software: you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by the Free
 * Software Foundation, either version 3 of the License, or (at your option)
 * any later version.
 *
 * CAcert votebot is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
 * or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
 * more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * CAcert votebot.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'application'
    id 'org.springframework.boot' version '2.6.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("commons-cli:commons-cli:1.5.0")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor:2.6.2")
}

group = 'org.cacert'
version = '0.3.0-SNAPSHOT'
description = "IRC vote bot for CAcert.org"

bootJar {
    mainClass = 'org.cacert.votebot.vote.CAcertVoteBot'
    launchScript()
}

springBoot {
    buildInfo()
}

compileJava.inputs.files(processResources)
compileJava.inputs.files(bootBuildInfo)

test {
    useJUnitPlatform()
}

bootRun {
    args System.getProperty("exec.args", "").split()
    mainClass = 'org.cacert.votebot.vote.CAcertVoteBot'
}
